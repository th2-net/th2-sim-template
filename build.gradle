buildscript {
    repositories {
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath "com.exactpro.th2:box-descriptor-generator:0.1.10"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
}

apply plugin: 'th2plugin'

parameters {
    namePatterns = ["grpc"]
    outputDirectory = "."
}

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

group 'com.exactpro.th2'
version release_version

repositories {
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenCentral()
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:3.0.0')

    implementation "com.exactpro.th2:sim:3.9.0"
    implementation "com.exactpro.th2:common:3.31.1"
    api "com.exactpro.th2:grpc-sim-template:3.1.2-demo-2184313879-SNAPSHOT"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.9'

    implementation "javax.annotation:javax.annotation-api:1.3.2"


    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation testFixtures("com.exactpro.th2:sim:3.9.0")
    testImplementation testFixtures("com.exactpro.th2:common:3.31.1")
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
        }
        kotlin {
            srcDirs 'src/main/kotlin'
        }
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = "com.exactpro.th2.sim.run.SimulatorServerMain"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

sourceCompatibility = 11
targetCompatibility = 11
