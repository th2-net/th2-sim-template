buildscript {
    repositories {
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath "com.exactpro.th2:box-descriptor-generator:0.1.10"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
}

apply plugin: 'th2plugin'

parameters {
    namePatterns = ["grpc"]
    outputDirectory = "."
}

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

group 'com.exactpro.th2'
version release_version

repositories {
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenCentral()
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.0.2')

    implementation "com.exactpro.th2:sim:5.2.3"
    implementation "com.exactpro.th2:common:$common_version"
    api "com.exactpro.th2:grpc-sim-template:3.1.2"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.9'

    implementation "javax.annotation:javax.annotation-api:1.3.2"

    //TODO: rework log dependency after common fix
    implementation "org.slf4j:slf4j-api:2.0.3"
    implementation ("org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0")
    implementation("org.apache.logging.log4j:log4j-1.2-api:2.19.0")
    implementation("org.apache.logging.log4j:log4j-api:2.19.0")
    implementation("org.apache.logging.log4j:log4j-core:2.19.0")

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation testFixtures("com.exactpro.th2:sim:5.2.3")
    testImplementation testFixtures("com.exactpro.th2:common:$common_version")
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc'
        }
        kotlin {
            srcDirs 'src/main/kotlin'
        }
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = "com.exactpro.th2.sim.run.SimulatorServerMain"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
    }
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

sourceCompatibility = 11
targetCompatibility = 11
